buildscript {
	dependencies {
		classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
		classpath 'net.minecraftforge.gradle:ForgeGradle:+'
	}

	repositories {
		maven {
			url 'https://files.minecraftforge.net/maven'
		}

		jcenter()
	}
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle'

archivesBaseName = 'circuitry'
group = 'cmsc389e.' + archivesBaseName
version = '1.15.2-1.1.0.0'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
compileJava.options.compilerArgs << '-proc:none'

minecraft {
	runs {
		client {
			mods {
				circuitry.source sourceSets.main
			}

			workingDirectory project.file('run')
		}

		server {
			mods {
				circuitry.source sourceSets.main
			}

			workingDirectory project.file('run')
		}
	}

	mappings channel: 'snapshot', version: '20200514-1.15.1'
}

reobf {
	shadowJar {}
}

dependencies {
	compile 'org.apache.httpcomponents:httpmime:4.3.3'
	minecraft 'net.minecraftforge:forge:1.15.2-31.2.0'
}

shadowJar {
	dependencies {
		include(dependency(':httpmime'))
	}

	classifier = ''
	manifest.attributes 'Implementation-Version': "${version}"
	minimize()
	relocate 'org.apache.http.entity.mime', project.group + '.org.apache.http.entity.mime'
}