buildscript {
	repositories {
		maven {
			url 'https://files.minecraftforge.net/maven'
		}
		
		jcenter()
	}
	
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:+'
	}
}

plugins {
	id 'com.github.johnrengelman.shadow' version '4.0.4'
}
apply plugin: 'net.minecraftforge.gradle'

archivesBaseName = 'circuitry'
group = 'cmsc389e.circuitry'
version = '1.15.2-1.0.0.0'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
compileJava.options.compilerArgs << '-proc:none'

minecraft {
	mappings channel: 'snapshot', version: '20200514-1.15.1'
	
	runs {
		client {
			workingDirectory project.file('run')
			
			mods {
				circuitry.source sourceSets.main
			}
		}
		
		server {
			workingDirectory project.file('run')
			
			mods {
				circuitry.source sourceSets.main
			}
		}
	}
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.15.2-31.2.0'
	compile 'org.apache.httpcomponents:httpmime:4.3.3'
}

reobf {
	shadowJar {
	}
}

shadowJar {
	dependencies {
		include(dependency(':httpmime'))
	}
	
 	classifier = ''
	manifest.attributes 'Implementation-Version': "${version}"
	minimize()
	relocate 'org.apache.http.entity.mime', project.group + '.org.apache.http.entity.mime'
}